runtime autoload/plug.vim
if exists("*plug#begin")
    call plug#begin('~/.vim/plugged')

    " Plugins
    Plug 'tpope/vim-eunuch'
    Plug 'tpope/vim-fugitive'
    Plug 'tpope/vim-rhubarb'
    Plug 'tpope/vim-vinegar'
    Plug 'tpope/vim-commentary'
    Plug 'tpope/vim-surround'
    Plug 'tpope/vim-unimpaired'
    Plug 'junegunn/fzf'
    Plug 'junegunn/fzf.vim'
    Plug 'dikiaap/minimalist'
    Plug 'neomake/neomake'
    Plug 'editorconfig/editorconfig-vim'

    " LSP
    Plug 'prabirshrestha/async.vim'
    Plug 'prabirshrestha/vim-lsp'
    Plug 'mattn/vim-lsp-settings'

    " Language specific plugins
    Plug 'vim-python/python-syntax'
    Plug 'Vimjas/vim-python-pep8-indent'
    Plug 'pangloss/vim-javascript'
    Plug 'posva/vim-vue'
    Plug 'octol/vim-cpp-enhanced-highlight'

    call plug#end()
endif

" -------------------- Base settings --------------------

" Syntax highlighting
syntax enable

" Load a .vimrc from current directory as well if present
set exrc

" Restict what can be done in current dir vimrc
set secure

" Key timeouts
set ttimeout
set ttimeoutlen=100
set timeout
set timeoutlen=500

" Auto reload buffers
set autoread

" Show line numbers
set number

" Show signs in number column
if has("patch-8.1.1564")
    set signcolumn=number
endif

" Number formats
set nrformats-=octal

" Enable mouse support
set mouse=a
set ttymouse=xterm2

" Never use the bell
set belloff=all

" Leader
let mapleader = "\<Space>"

" Backspace over anything
set backspace=indent,eol,start

" Statusline
set laststatus=2

" Tags
set tags^=.git/tags

" Command history
set history=200

" Echo commands
set showcmd

" Show incremental search
set incsearch

" Patterns ignored when using wildcards
set wildignore=*.exe,*.dll,*.so,*.pyc
set wildignore+=*/.git/objects/*
set wildignore+=*/.git/logs/*
set wildignore+=*/.git/refs/*
set wildignore+=*/__pycache__/*
set wildignore+=*__pycache__
set wildignore+=*/.mypy_cache/*

" Path
set path=.,**

" Line display
set display=lastline

" Grep settings
set grepformat=%f:%l:%c:%m,%f:%l:%m
if isdirectory(".git")
    set grepprg=git\ grep\ --untracked\ --perl-regexp\ --line-number\ --column
elseif executable("rg")
    set grepprg=rg\ --vimgrep\ --smart-case
endif

" Command completion
set wildmenu
if has("patch-8.1.2225")
    set wildmode=list:lastused,full
else
    set wildmode=list,full
endif

" Completion
set completeopt=menu,menuone,noinsert
set complete=.,w,b,u,t
set pumheight=10
if has("patch-8.1.1880")
    " Enable popups for completion items in recent vim
    set completeopt+=popup
endif

" Ignore case when searching except when caps used
set ignorecase
set smartcase

" Allow switching from unsaved buffer
set hidden

" Turn off backups
set nobackup
set nowritebackup
set noswapfile

" Give cursor 7 lines
set scrolloff=7

" Turn off modelines
set nomodeline

" Spelling
set spelllang=en_au,en_us
if has("patch-8.2.0953")
    set spelloptions=camel
endif
set dictionary=/usr/share/dict/words

" Chars for list mode
set listchars=tab:>\ ,trail:-,extends:>,precedes:<,nbsp:+

" Always show how many lines affected
set report=0

" Let vim set window title
set title

" Data saved with session
set sessionoptions-=options

" Indentation defaults
set expandtab
set softtabstop=-1
set shiftwidth=4

" Folding
set foldlevel=99
set foldmethod=indent
set foldnestmax=20

" Windows
set equalalways
set eadirection="hor"

" Show column at textwidth +1 if textwidth is set
set colorcolumn=+1

" GUI
set guioptions=PdimgtrL

" Formatting (see :h fo-table)
set formatoptions=crqnlj
set formatlistpat="\v^\s*(\d+[\]:\.\)\}\t ]|[\-\+\*]\s)\s*"

" Colors
set background=dark

" Set underline curl and color sequences
if has('patch-8.2.0863')
    let &t_AU = "\e[58;5;%dm"
endif
let &t_Cs = "\e[4:3m"
let &t_Ce = "\e[4:0m"
" Use background colour on clear
let &t_ut = ""

" Always redraw screen on scroll
if exists('&ttyscroll')
    set ttyscroll=1
endif

" Highlights
augroup highlights
    autocmd!
    autocmd colorscheme *
        \ highlight link Terminal Normal |
        \ highlight DiffAdded cterm=NONE ctermfg=64 gui=NONE guifg=#5f8700 |
        \ highlight DiffRemoved cterm=NONE ctermfg=167 gui=NONE guifg=#d75f5f |
        \ highlight link pythonClassVar Function |
        \ highlight StatusLineTerm cterm=bold ctermfg=150 ctermbg=239 gui=bold guifg=#afd787 guibg=#4e4e4e |
        \ highlight StatusLineTermNC cterm=NONE ctermfg=150 ctermbg=239 gui=NONE guifg=#afd787 guibg=#4e4e4e |
        \ highlight User1 cterm=bold ctermfg=255 ctermbg=59 gui=bold guifg=#eeeeee guibg=#5f5f5f |
        \ highlight User2 cterm=bold ctermbg=24 gui=bold guibg=#005f87 |
        \ highlight User3 cterm=bold ctermbg=239 ctermfg=167 gui=bold guibg=#4e4e4e guifg=#d75f5f

    if has("patch-8.2.0863")
        autocmd colorscheme *
            \ highlight SpellBad cterm=undercurl ctermul=167 ctermfg=NONE ctermbg=NONE |
            \ highlight SpellCap cterm=undercurl ctermul=74 ctermfg=NONE ctermbg=NONE |
            \ highlight SpellLocal cterm=undercurl ctermul=14 ctermfg=NONE ctermbg=NONE |
            \ highlight SpellRare cterm=undercurl ctermul=140 ctermfg=NONE ctermbg=NONE
    endif

    autocmd colorscheme minimalist
        \ highlight SignColumn ctermbg=234 |
        \ highlight PMenu ctermbg=237
augroup END

" Colorscheme with fallback
try
    colorscheme minimalist
catch
    colorscheme desert
endtry

" Move cursor to last position when opening file if appropriate
" Taken from defaults.vim
augroup position
    autocmd!
    autocmd BufReadPost *
          \ if line("'\"") >= 1 && line("'\"") <= line("$") && &ft !~# 'commit'
          \ |   exe "normal! g`\""
          \ | endif
augroup END

" Statusline, plus functions used in statusline
function! GetModified()
    if !&modifiable
        return "[NM]"
    endif
    if &modified
        return "[Modified]"
    endif
    return ""
endfunction

function! GetBranchName()
    if exists("*FugitiveHead")
        let branchName = FugitiveHead()
        if len(branchName)
            return "[" . branchName . "] "
        endif
    endif
    return ""
endfunction

set statusline=
set statusline+=%1*
set statusline+=\ %n\                      " Buffer number
set statusline+=%{GetBranchName()}\        " Git branch
set statusline+=%2*
set statusline+=%(\ %h%w%q\ %)             " Flags
set statusline+=%3*
set statusline+=%(\ %{GetModified()}%r%)  " Error Flags
set statusline+=%<%0*
set statusline+=\ %f                       " File name
set statusline+=%=                         " Seperate left and right
set statusline+=%2*
set statusline+=\ %y\                      " Type
set statusline+=%1*
set statusline+=\ %LL\ %p%%\               " Stats

" Netrw settings
let g:netrw_banner = 0
let g:netrw_liststyle = 1
let g:netrw_browse_split = 0
let g:netrw_altv = 1
let g:netrw_altfile = 1
let g:netrw_sizestyle = "h"
let g:netrw_fastbrowse = 0

" -------------------- Functions --------------------

" Stips whitespace while maintaining cursor position
function! StripTrailingWhitespace()
  if !&binary && &filetype != 'diff'
    normal mz
    normal Hmy
    %s/\s\+$//e
    normal 'yz<CR>
    normal `z
  endif
endfunction

" Format file with gq
function! FormatFile()
    normal mz
    normal Hmy
    normal gggqG
    normal 'yz<cr>
    normal 'z
endfunction

" If there is a window at pos close it
function! CloseWin(pos)
    let cur = winnr()
    let id = winnr(a:pos)
    if cur != id
        execute id . "wincmd c"
    endif
endfunction

" Toggle auto formatting
function! ToggleAutoFormat()
    if stridx(&formatoptions, "a") == -1
        setlocal formatoptions+=a
        echomsg "Auto format on"
    else
        setlocal formatoptions-=a
        echomsg "Auto format off"
    endif
endfunction

" Copy value to clipboard and print
function! CopyAndPrint(value)
    let @+ = a:value
    echo @+
endfunction

" Make session and quit
function! SaveAndQuit()
    mks! ~/session.vim
    qall
endfunction

" -------------------- Commands --------------------

command! -nargs=1 -complete=dir Tabdir :tabnew | lcd <args> | Ex
command! -nargs=+ Gp silent! grep! <args> | redraw! | botright copen
command! -nargs=+ Gpw Gp -w <args>
command! -nargs=1 Type setlocal filetype=<args>
command! -nargs=1 Fileat Gedit <args>:%
command! AutoFormat call ToggleAutoFormat()
command! PrTemplate read .github/pull_request_template.md
command! Bufonly %bd | e#
command! SessionSave mks! ~/session.vim
command! Quit call SaveAndQuit()
command! Retag Run ./.git/hooks/ctags
" Set argument list to files edited
command! Changed execute "args! " . join(systemlist("git ls-files --modified --others --exclude-standard"), " ")
" Import sort (sort by second word)
command! -range Isort <line1>,<line2>sort /\w\+ /
" Wrapper on neomake
command! Make Neomake! | bo copen
" Prompt for markdown checkboxes
command! Check %s/\[ \]/[x]/gc

" -------------------- Mappings --------------------

" Formatting
map Q gq
" FZF
nnoremap <C-p> :GFiles<cr>
" Move around
nnoremap <C-j> <C-W>j
nnoremap <C-k> <C-W>k
nnoremap <C-h> <C-W>h
nnoremap <C-l> <C-W>l
" Strip whitespace
nnoremap <F2> :call StripTrailingWhitespace()<cr>
" Check files to reload
nnoremap <F5> :checktime<cr>
" Disable ale for this duffer
nnoremap <F6> :NeomakeToggleBuffer <bar> NeomakeClean<cr>
" Format file
nmap <F8> :call FormatFile()<cr>
" Edit
nnoremap <leader>e :e **/*
nmap <leader>ef <Plug>NavigateEditCursor
" Change indentation
nnoremap <leader>i2 :setlocal shiftwidth=2<cr>
nnoremap <leader>i4 :setlocal shiftwidth=4<cr>
" Paste
nnoremap <leader>p "+]p
" Open terminal
nnoremap <leader>tb :TmuxSplit<cr>
nnoremap <leader>tt :TmuxWindow<cr>
" Search mappings
nnoremap <leader>ff :find **/*
nnoremap <leader>fb :Buffers<cr>
nnoremap <leader>ft :Tags<cr>
" Git maps
nnoremap <leader>gs :Gstatus<cr>
nnoremap <leader>gd :Gdiff<cr>
nnoremap <leader>gb :Gblame<cr>
" Grep word under cursor
nnoremap <leader>r :Gpw <C-r><C-w>
" Substitute word under cursor
nnoremap <leader>s :%s/\<<cword>\>/
" Copy file path to register
nnoremap <leader>yf :call CopyAndPrint(expand("%"))<cr>
" Save
nnoremap <leader>w :w<cr>
" Language server
nnoremap <leader>] :LspDefinition<cr>
nnoremap <leader>ld :LspDocumentDiagnostics<cr>
nnoremap <leader>lh :LspHover<cr>
nnoremap <leader>ln :LspRename<cr>
nnoremap <leader>lr :LspReferences<cr>
nnoremap <leader>la :LspCodeAction<cr>
nnoremap <leader>ls :LspDocumentSymbol<cr>
nnoremap <leader>lw :LspWorkspaceSymbol<cr>

" Insert mode maps
imap <C-@> <C-Space>
imap <C-Space> <C-x><C-o>
inoremap <expr> <C-k> pumvisible() ? "\<Up>" : "\<C-k>"
inoremap <expr> <C-j> pumvisible() ? "\<Down>" : "\<C-j>"

" Visual mode maps
vnoremap <leader>s :s/\<<C-r>0\>/
vnoremap <leader>y "+y

" Fix for c-left and c-right under tmux
map  <Esc>[1;5D <C-Left>
map  <Esc>[1;5C <C-Right>
cmap  <Esc>[1;5D <C-Left>
cmap  <Esc>[1;5C <C-Right>

" -------------------- Plugin Settings --------------------

" Syntax
let g:vim_json_warnings = 0
let g:python_highlight_all = 0
let g:python_highlight_space_errors = 0
let g:python_highlight_builtin_objs = 1
let g:python_highlight_builtin_funcs = 1
let g:python_highlight_builtin_types = 1
let g:python_highlight_class_vars = 1
let g:python_highlight_operators = 1
let g:python_highlight_exceptions = 1
let g:cpp_class_scope_highlight = 1
let g:cpp_member_variable_highlight = 1
let g:cpp_class_decl_highlight = 1

" LSP
let g:lsp_settings_enable_suggestions = 0

" Function called when each buffer enabled
function! s:on_lsp_buffer_enabled() abort
    setlocal omnifunc=lsp#complete
endfunction

autocmd User lsp_buffer_enabled call s:on_lsp_buffer_enabled()

let g:lsp_diagnostics_enabled = 0
let g:lsp_highlights_enabled = 0
let g:lsp_textprop_enabled = 0
let g:lsp_preview_doubletap = 0
let g:lsp_signature_help_enabled = 0
let g:lsp_documentation_float = 0
let g:lsp_preview_float = 1
let g:lsp_diagnostics_echo_cursor = 1
let g:lsp_peek_alignment = "top"
let g:lsp_highlight_references_enabled = 0

" Format go files on save
autocmd BufWritePre *.go :silent LspDocumentFormatSync

" Neomake

" Temp file location
let g:neomake_tempfile_dir = '/tmp/neomake%:p:h'

" Automake when reading / editing (after 100ms), and when writing.
runtime autoload/neomake/configure.vim
if exists('*neomake#configure#automake')
    call neomake#configure#automake('nrw', 100)
endif

" Highlights
let g:neomake_highlight_columns = 0
let g:neomake_error_sign = {
 \ 'text': 'E>',
 \ 'texthl': 'Error',
 \ }
let g:neomake_warning_sign = {
 \   'text': 'W>',
 \   'texthl': 'Todo',
 \ }
let g:neomake_message_sign = {
  \   'text': 'M>',
  \   'texthl': 'Normal',
  \ }
let g:neomake_info_sign = {
  \ 'text': 'I>',
  \ 'texthl': 'Normal'
  \ }

" Custom makers
let g:neomake_cloudformation_cfnlint_maker = {
    \ 'exe': 'cfn-lint',
    \ 'args': ['--template', '%t', '--format', 'parseable'],
    \ 'append_file': 0,
    \ 'errorformat': '%f:%l:%c:%*\d:%*\d:%m'
    \ }


" Enabled makers
function! s:addLinterIfExists(list, linter)
    if executable(a:linter)
        " Assume maker name is same as program with - and _ removed
        let maker_name = substitute(a:linter, '-\|_', '', 'g')
        call add(a:list, maker_name)
    endif
endfunction

let g:neomake_python_enabled_makers = []
call s:addLinterIfExists(g:neomake_python_enabled_makers, 'flake8')
call s:addLinterIfExists(g:neomake_python_enabled_makers, 'mypy')
let g:neomake_javascript_enabled_makers = []
call s:addLinterIfExists(g:neomake_javascript_enabled_makers, 'eslint')
let g:neomake_vue_enabled_makers = []
call s:addLinterIfExists(g:neomake_vue_enabled_makers, 'eslint')
let g:neomake_cloudformation_enabled_makers = []
call s:addLinterIfExists(g:neomake_cloudformation_enabled_makers, 'cfn-lint')
let g:neomake_go_enabled_makers = []
call s:addLinterIfExists(g:neomake_go_enabled_makers, 'go')
call s:addLinterIfExists(g:neomake_go_enabled_makers, 'govet')
let g:neomake_cpp_enabled_makers = []
call s:addLinterIfExists(g:neomake_cpp_enabled_makers, 'cppcheck')
call s:addLinterIfExists(g:neomake_cpp_enabled_makers, 'cpplint')

" Linter args
let g:neomake_mypy_args = [
    \ '--show-column-numbers',
    \ '--config-file',
    \ '~/.config/mypy/config',
    \ ]
let g:neomake_yamllint_args = [
    \ '-f',
    \ 'parsable',
    \ ]
let g:neomake_eslint_args = [
    \ '--format=compact'
    \ ]

" Load matchit plugin (comes with vim)
if !exists('g:loaded_matchit')
    runtime! macros/matchit.vim
endif

" Load :Man command (comes with vim)
runtime! ftplugin/man.vim

" FZF
let g:fzf_layout = {'window': {'width': 0.8, 'height': 0.6}}
let g:fzf_preview_window = ''

" Editorconfig
let g:EditorConfig_exclude_patterns = ['fugitive://.*']
